/*  Copyright (c) 2015, LAAS/CNRS
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without 
 * modification, are permitted provided that the following conditions are met:
 * 
 * 1. Redistributions of source code must retain the above copyright notice, 
 *    this list of conditions and the following disclaimer.
 * 
 * 2. Redistributions in binary form must reproduce the above copyright notice, 
 *    this list of conditions and the following disclaimer in the documentation 
 *    and/or other materials provided with the distribution.
 * 
 * 3. Neither the name of the copyright holder nor the names of its contributors 
 *    may be used to endorse or promote products derived from this software 
 *    without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" 
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE 
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE 
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE 
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR 
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF 
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS 
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) 
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE 
 * POSSIBILITY OF SUCH DAMAGE.
 */

#include "kemar.idl"
component kemar {
    version     "1.0";
    lang        "c";
    require     "genom3 >= 2.99.24";
    codels-require "elmo-axis-libs";

    port out kemar::state currentState;
	port out kemar::position_indexes_movement Indexes;

    task motion {
        codel <start>   motionStart(port out Indexes) yield ether;
        codel <stop>    motionStop() yield ether;
    };

    task state {
        period                  2ms;
        priority                400;
        stack                   4000;

        codel <start>           stateStart() yield sendS;
        codel <sendS>           sSend(port out currentState) yield recvS, sendS;
        async codel <recvS>     sWaitForData() yield sendS, recvS;
    };

    /*Homing: Calls kemarHoming(h) and k=KemarStructInit(h)*/
    activity Homing() {
        task motion;

        codel <start>           hStart() yield sendH;
        codel <sendH>           hSend() yield recvH, ether;
        async codel <recvH>     hWaitForData() yield sendH, recvH, ether;
    };

    /*Current Position: calls kemarGetInfo*/
    activity CurrentPosition() {
        task motion;

        codel <start>           cpStart() yield sendCP;
        codel <sendCP>          cpSend(port out currentState) yield recvCP, sendCP, ether;
        async codel <recvCP>    cpWaitForData() yield sendCP, recvCP;
    };

    /*Stop Current position*/
    activity StopCurrentPosition() {
        task motion;

        codel <start>           scpStart() yield ether;
    };

    /*Set Velocity: calls kemarSetGearVelRadS*/
    activity SetVelocity(in double velocity) {
        task motion;

        codel <start>           svStart(in velocity) yield ether;
    };

    /*Move Absolute Position: calls kemarSetGearVelRadS, kemarSetGearPosAbsRad, kemarWaitMsgValid*/
    activity MoveAbsolutePosition(in double target) {
        task motion;

        codel <start>           mapStart() yield sendMAP;
        codel <sendMAP>         mapSend(in target, port out Indexes) yield recvMAP, ether; 
        async codel <recvMAP>   mapWaitForData() yield sendMAP, recvMAP;
    };

    /*Move Relative Position: calls kemarSetGearVelRadS, kemarSetGearPosRelRad, kemarWaitMsgValid*/
    activity MoveRelativePosition(in double target) {
        task motion;

        codel <start>           mrpStart() yield sendMRP;
        codel <sendMRP>         mrpSend(in target) yield recvMRP, ether;
        async codel <recvMRP>   mrpWaitForData() yield sendMRP, recvMRP;
    };

    /*Control in Speed (Reads Velocity to set): calls kemarSetGearVelRadS*/
    activity ControlInSpeed(in double velocity) {
        task motion;

        codel <start>           cisStart() yield sendCIS;
        codel <sendCIS>         cisSend(in velocity) yield recvCIS, ether;
        async codel<recvCIS>    cisWaitForData() yield sendCIS, recvCIS;
        
    };
};
